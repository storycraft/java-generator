/*
 * Created on Mon Feb 06 2023
 *
 * Copyright (c) storycraft. Licensed under the Apache Licence 2.0.
 */

plugins {
    id 'java-library'
    id 'eclipse'
}

group = 'sh.pancake'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'org.projectlombok:lombok:1.18.26'
	annotationProcessor 'org.projectlombok:lombok:1.18.26'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testAnnotationProcessor project(':lib')
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    testLogging {
        showStandardStreams = true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += [
        '--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
    ]
}

eclipse.classpath.file {
    whenMerged {
        entries.find{ it.path ==~ '.*JRE_CONTAINER.*' }.each {
            it.entryAttributes['module'] = true
            it.entryAttributes['add-exports'] = 'jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED:' +
            'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED:' +
            'jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED:' +
            'jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED:' +
            'jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED:' +
            'jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED:' +
            'jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED'
        }
    }
}